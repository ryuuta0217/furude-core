import io.papermc.paperweight.userdev.ReobfArtifactConfiguration

plugins {
    id 'java'
    id 'io.papermc.paperweight.userdev' version '1.7.+'
    id 'xyz.jpenilla.run-paper' version '2.+'
    id 'com.github.johnrengelman.shadow' version '+'
}

group = 'com.ryuuta0217'
version = '3.5.3'

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
}

dependencies {
    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:1.21-R0.1-SNAPSHOT")
    implementation group: 'org.mozilla', name: 'rhino', version: '+'
}

paperweight {
    reobfArtifactConfiguration = ReobfArtifactConfiguration.getMOJANG_PRODUCTION()
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

runServer {
    jvmArgs("-Xmx768M")
}

tasks.build.dependsOn(reobfJar)

jar {
    archiveFileName = "original-" + archiveBaseName.get() + "-" + archiveVersion.get() + "." + archiveExtension.get()
}

shadowJar {
    archiveFileName = archiveBaseName.get() + "-" + archiveVersion.get() + "." + archiveExtension.get()
}

reobfJar {
    outputJar = layout.buildDirectory.file("libs/${project.name}-${project.version}-spigot.jar")
}